#!ipxe

#########################################################################
# File: boot.ipxe
# Created by: Dmitri G.
# Date: 2019-10-22
# Description: This Command File Run iPXE routine
#########################################################################

echo
echo .........................................
echo . \/\/\/ Runing the iPXE routine /\/\/\ .
echo . ************** v1.0b **************** .
echo . /\/\ Created By: Dmitri G. (2019) /\/ .
echo .........................................
echo

# Get IP address
dhcp

# Some menu defaults
set menu-timeout 30000
set submenu1-default alpine-x86
set submenu2-default clonezilla64
set submenu1-timeout ${menu-timeout}
set submenu2-timeout ${menu-timeout}
isset ${menu-default} || set menu-default exit

# Variables setup (set your HTTP server IP address)
# General
set protocol http
set server_ip xx.xx.x.xx
set console tty0
set alpine_cmdline modules=loop,squashfs quiet nomodeset
set alpine_flavor vanilla

# Distros names and versions (change names to actual ones)
set _arch_ver 2019.10.01
set centos_x86_64 CentOS-8-x86_64-1905-boot.iso
set clonezilla_amd64 clonezilla-live-2.6.3-7-amd64.iso
set dban_iso dban-2.3.0_i586.iso

# Network location (change links to actual ones)
set base ${protocol}://${server_ip}/boot
set arch_int_base ${protocol}://mirror.yandex.ru/archlinux/iso
set centos_int_base ${protocol}://mirror.yandex.ru/centos/8.0.1905/isos/x86_64/${centos_x86_64}
set alpine_x86_base ${protocol}://${server_ip}/boot/distro/alpine-extended-3.10.2-x86
set alpine_x86_64_base ${protocol}://${server_ip}/boot/distro/alpine-extended-3.10.3-x86_64

###################
:boot_menu
###################
menu *** MAIN MENU ***
item --gap --             ------------------------- Operating systems ------------------------------
item --key w winpe        Windows PE
item --gap --             ------------------------- Tools and Utilities ----------------------------
item --key i menu-install Installers...
item --key t menu-tools   Tools...
item --key d dban         DBAN
item --gap --             ------------------------- Advanced options -------------------------------
item --key r reboot       Reboot computer
item --key p poweroff     Poweroff computer
item
item --key x exit         Exit and continue BIOS/UEFI boot

choose --timeout ${menu-timeout} --default ${menu-default} target || goto cancel
set menu-timeout 0
goto ${target}

:back
set submenu1-timeout 0
clear submenu1-default
set submenu2-timeout 0
clear submenu2-default
goto boot_menu

:failed
echo Booting failed, Dropping to shell !!!
goto shell

:cancel
echo You have cancelled the menu, dropping you to a shell

:shell
echo Your IP is (IP::GateWay:NetMask): ${net0/ip}::${net0/gateway}:${net0/netmask}
echo Type 'exit' to get the back to the MENU !!!
shell
set menu-timeout 0
goto boot_menu

:reboot
reboot

:poweroff
poweroff

:exit
exit

############ MAIN MENU ITEMS ############

:winpe
echo Running WinPE...

kernel ${base}/wimboot
initrd ${base}/winpe/media/Boot/BCD          BCD
initrd ${base}/winpe/media/Boot/boot.sdi     boot.sdi
initrd ${base}/winpe/media/sources/boot.wim  boot.wim
boot || goto failed

:dban
echo Running DBAN...

kernel ${base}/memdisk
initrd ${base}/iso/${dban_iso}
imgargs memdisk iso raw
boot || goto failed

############################ INSTALLER MENU #########################

:menu-install
menu *** INSTALLERs MENU ***
item --gap --             ------------------------- Linux installers -------------------------------
item alpine-x86           Alpine Linux x86 (local)
item alpine-x86_64        Alpine Linux x86_64 (local)
item arch64               Archlinux-x86_64 (internet)
item centos64             CentOS-8-x86_64 (internet)
item
item --key 0x08 back      Back to *** MAIN MENU ***
isset ${submenu1-default} && goto menu-install-timed ||
choose target && goto ${target} || goto boot_menu
:menu-install-timed
choose --timeout ${submenu1-timeout} --default ${submenu1-default} target && goto ${target} || goto boot_menu

:arch64
echo Running Arch Linux x86_64 internet installer...
echo -n Enter Arch Linux version DATE as YYYY.MM.DD (current ${_arch_ver}): && read arch_ver
isset ${arch_ver} || set arch_ver ${_arch_ver}

kernel ${base}/memdisk
initrd ${arch_int_base}/${arch_ver}/archlinux-${arch_ver}-x86_64.iso
imgargs memdisk iso raw
boot || goto failed

:alpine-x86
echo Running Alpine Linux i686 installer...
set modloop-url ${alpine_x86_base}/boot/modloop-${alpine_flavor}

kernel ${alpine_x86_base}/boot/vmlinuz-${alpine_flavor} ${alpine_cmdline} alpine_repo=${alpine_x86_base}/apks/ modloop=${modloop-url} ${console}
initrd ${alpine_x86_base}/boot/initramfs-${alpine_flavor}
boot || goto failed

:alpine-x86_64
echo Running Alpine Linux x86_64 installer...
set modloop-url ${alpine_x86_64_base}/boot/modloop-${alpine_flavor}

kernel ${alpine_x86_64_base}/boot/vmlinuz-${alpine_flavor} ${alpine_cmdline} alpine_repo=${alpine_x86_64_base}/apks/ modloop=${modloop-url} ${console}
initrd ${alpine_x86_64_base}/boot/initramfs-${alpine_flavor}
boot || goto failed

:centos64
echo Running CentOS Linux x86_64 internet installer...

kernel ${base}/memdisk
initrd ${centos_int_base}
imgargs memdisk iso raw
boot || goto failed

###################### RECOVERY MENU ################################

:menu-tools
menu *** RECOVERY TOOLs MENU ***
item --gap --             ------------------------- Cloning Tools ----------------------------------
item clonezilla64         Clonezilla-AMD64
item --gap --             ------------------------- Tools ------------------------------------------
item pxelinux             Load PXELinux menu
item
item --key 0x08 back      Back to *** MAIN MENU ***
isset ${submenu2-default} && goto menu-recovery-timed ||
choose target && goto ${target} || goto boot_menu
:menu-recovery-timed
choose --timeout ${submenu2-timeout} --default ${submenu2-default} target && goto ${target} || goto boot_menu

:clonezilla64
echo Running Clonezilla 64bit from internet...

kernel ${base}/memdisk
initrd ${base}/iso/${clonezilla_amd64}
imgargs memdisk iso raw
boot || goto failed

:pxelinux
set 210:string tftp://${next-server}/
chain ${210:string}pxelinux.0 || goto failed

