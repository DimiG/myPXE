#!ipxe

#########################################################################
# File: boot.ipxe
# Created by: Dmitri G.
# Date: 2019-10-17
# Description: This Command File Run iPXE routine
#########################################################################

echo
echo .........................................
echo . \/\/\/ Runing the iPXE routine /\/\/\ .
echo . ************** v1.0b **************** .
echo . /\/\ Created By: Dmitri G. (2019) /\/ .
echo .........................................
echo

# Get IP address
dhcp

# Some menu defaults
set menu-timeout 30000
set menu-default shell
isset ${menu-default} || set menu-default shell

# Variables setup (set your HTTP server IP address)
set protocol http
set server_ip xx.xx.x.xx
set dban_iso dban-2.3.0_i586.iso
set arch_x86_64 archlinux-2019.10.01-x86_64.iso
set arch_i686 archlinux-2019.10.07-i686.iso
set clonezilla_amd64 clonezilla-live-2.6.3-7-amd64.iso
set clonezilla_i686 clonezilla-live-2.6.3-7-i686.iso
set base ${protocol}://${server_ip}/boot
set arch_base ${protocol}://${server_ip}/boot/archlinux-2019.10.07-i686
set int_base http://mirror.yandex.ru/archlinux/iso

###################
:boot_menu
###################
menu *** MAIN MENU ***
item --gap Windows Tools
item --key w winpe Windows PE
item --gap Tools
item clonezilla64 clonezilla-live-amd64 ISO boot
item clonezilla32 clonezilla-live-i686 ISO boot
item --key d dban DBAN
item --gap Linux installers
item arch64 archlinux-x86_64 ISO boot
item arch32 archlinux-i686 ISO boot
item arch_i686 archlinux-i686 boot
item --gap Linux internet installers
item arch64int archlinux-x86_64 INTERNET boot
item --gap iPXE tools
item shell Drop to iPXE shell
item ipxe_dhcp Get IP by DHCP
item --key r reboot Reboot computer
item --key p poweroff Poweroff computer
item
item --key x exit Exit iPXE and continue BIOS/UEFI boot

choose --timeout ${menu-timeout} --default ${menu-default} target || goto cancel
set menu-timeout 0
goto ${target}

###################
# MAIN MENU ITEMS
###################

:failed
echo Booting failed, Dropping to shell !!!
goto shell

:reboot
reboot

:poweroff
poweroff

:exit
exit

:ipxe_dhcp
echo Run IP request...
dhcp
goto boot_menu

:cancel
echo You have cancelled the menu, dropping you to a shell

:shell
echo Type 'exit' to get the back to the MENU !!!
shell
set menu-timeout 0
goto boot_menu

:winpe
echo Running WinPE...

kernel ${base}/wimboot
initrd ${base}/winpe/media/Boot/BCD          BCD
initrd ${base}/winpe/media/Boot/boot.sdi     boot.sdi
initrd ${base}/winpe/media/sources/boot.wim  boot.wim
boot || goto failed

:clonezilla64
echo Running Clonezilla 64bit...

kernel ${base}/memdisk || read void
initrd ${base}/iso/${clonezilla_amd64} || read void
imgargs memdisk iso raw || read void
boot || goto failed

:clonezilla32
echo Running Clonezilla 32bit...

kernel ${base}/memdisk || read void
initrd ${base}/iso/${clonezilla_i686} || read void
imgargs memdisk iso raw || read void
boot || goto failed

:dban
echo Running DBAN...

kernel ${base}/memdisk || read void
initrd ${base}/iso/${dban_iso} || read void
imgargs memdisk iso raw || read void
boot || goto failed

:arch64
echo Running Arch Linux x86_64 installer...

kernel ${base}/memdisk || read void
initrd ${base}/iso/${arch_x86_64} || read void
imgargs memdisk iso raw || read void
boot || goto failed

:arch32
echo Running Arch Linux i686 installer...

kernel ${base}/memdisk || read void
initrd ${base}/iso/${arch_i686} || read void
imgargs memdisk iso raw || read void
boot || goto failed

:arch_i686
echo Running Arch Linux i686 installer...
echo Your IP is (IP::GateWay:NetMask): ${net0/ip}::${net0/gateway}:${net0/netmask}
set params archiso_http_srv=${arch_base}/ archisobasedir=arch ip=${net0/ip}::${net0/gateway}:${net0/netmask}
kernel ${base}/vmlinuz ${params} initrd=archiso.img
initrd ${base}/archiso.img
boot || goto failed

:arch64int
echo Running Arch Linux x86_64 internet installer...
echo -n Arch Linux version. Enter version DATE as 2019.10.01: && read arch_ver
isset ${arch_ver} || set arch_ver 2019.10.01

kernel ${base}/memdisk || read void
initrd ${int_base}/${arch_ver}/archlinux-${arch_ver}-x86_64.iso || read void
imgargs memdisk iso raw || read void
boot || goto failed

